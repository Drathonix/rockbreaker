import com.drathonix.rockbreakerplugin.APIModInfo
import com.drathonix.rockbreakerplugin.APISource
import java.util.Optional
import java.util.function.BiConsumer
import java.util.function.Consumer
import java.util.function.Predicate
import com.drathonix.rockbreakerplugin.EnvType
import com.drathonix.rockbreakerplugin.DepType

// Baseline code. Minimal edits necessary.
// TODO acknowledge that you add plugins here.
plugins {
    `maven-publish`
    kotlin("jvm") version "2.1.0"
    //id("fabric-loom") // Leaving this here if you want to swap loom.
    id("dev.architectury.loom")
    //id("dev.kikugie.j52j") // Recommended by kiku if using swaps in json5.
    id("me.modmuss50.mod-publish-plugin")
    // The rockbreaker plugin, simply keeps all the backend junk separate.
    id("rockbreaker")
}

// Leave this alone unless adding more dependencies.
// TODO acknowledge that you add dependency repositories here.
repositories {
    mavenCentral()
    // Use these mavens to download mod source from curseforge and modrinth if no other option is available.
    // Example properties using modrinth maven for c2me: https://github.com/Drathonix/LoadMyChunks/blob/stonecutter/versions/1.21.1-neoforge/gradle.properties
    exclusiveContent {
        forRepository { maven("https://www.cursemaven.com") { name = "CurseForge" } }
        filter { includeGroup("curse.maven") }
    }
    exclusiveContent {
        forRepository { maven("https://api.modrinth.com/maven") { name = "Modrinth" } }
        filter { includeGroup("maven.modrinth") }
    }
    maven("https://maven.neoforged.net/releases/")
    maven("https://maven.architectury.dev/")
    maven("https://modmaven.dev/")
    maven("https://maven.ryuutech.com/repository/maven-releases/")
}

loom {
    silentMojangMappingsLicense()
    if (rockbreaker.env.isForge) forge {
        for (mixin in rockbreaker.modMixins.getMixins(EnvType.FORGE)) {
            mixinConfigs(
                mixin
            )
        }
    }

    decompilers {
        get("vineflower").apply { // Adds names to lambdas - useful for mixins
            options.put("mark-corresponding-synthetics", "1")
        }
    }

    runConfigs.all {
        ideConfigGenerated(stonecutter.current.isActive)
        vmArgs("-Dmixin.debug.export=true")
        runDir = "../../run"
    }
}
base { archivesName.set(rockbreaker.env.archivesBaseName) }

// Dependencies should be added this way. Stonecutter constants and dependencies will be autogenerated.
// Assuming that you have provided a repository to retrieve the mod code from rockbreaker will automatically download
// the dependency and add it to your mods dependency list if necessary.
rockbreaker.addAPI(
    APISource(
        DepType.API,
        APIModInfo(if (rockbreaker.atMost("1.17.1")) "fabric" else "fabric-api", "fabric-api"),
        "net.fabricmc.fabric-api:fabric-api",
        rockbreaker.optionalVersionProperty("deps.api.fabric")
    // This checks if the mod is enabled in the current stonecutter project. If false the dependency will not be
    // included in the mod dependency list on build and will not be listed as a dependency on publish.
    ) { src ->
        src.versionRange.isPresent && rockbreaker.env.isFabric
    }
)
rockbreaker.addAPI(
    APISource(
        DepType.API,
        APIModInfo("architectury", "architectury-api"),
        "${if (rockbreaker.atLeast("1.17.1")) "dev.architectury" else "me.shedaniel"}:architectury-${rockbreaker.env.loader}",
        rockbreaker.optionalVersionProperty("deps.api.architectury")
    )
    { src ->
        src.versionRange.isPresent
    }
)

dependencies {
    minecraft("com.mojang:minecraft:${rockbreaker.env.mcVersion.min}")
    // TODO do you really want to use yarn though? Like what convenience does it even give you smh?
    mappings(loom.officialMojangMappings())

    if(rockbreaker.env.isFabric) {
        modImplementation("net.fabricmc:fabric-loader:${rockbreaker.env.fabricLoaderVersion.min}")
    }
    if(rockbreaker.env.isForge){
        "forge"("net.minecraftforge:forge:${rockbreaker.env.forgeMavenVersion.min}")
    }
    if(rockbreaker.env.isNeo){
        "neoForge"("net.neoforged:neoforge:${rockbreaker.env.neoforgeVersion.min}")
    }

    rockbreaker.getAPIs().forEach { src->
        if(src.enabled) {
            src.versionRange.ifPresent { ver ->
                if(src.type == DepType.API || src.type == DepType.API_OPTIONAL) {
                    modApi("${src.mavenLocation}:${ver.min}")
                }
                if(src.type == DepType.IMPL) {
                    modImplementation("${src.mavenLocation}:${ver.min}")
                }
                if(src.type == DepType.FRL && rockbreaker.env.isForge){
                    "forgeRuntimeLibrary"("${src.mavenLocation}:${ver.min}")
                }
                if(src.type == DepType.INCLUDE) {
                    modImplementation("${src.mavenLocation}:${ver.min}")
                    include("${src.mavenLocation}:${ver.min}")
                }
            }
        }
    }

    vineflowerDecompilerClasspath("org.vineflower:vineflower:1.10.1")
}

project.tasks.getByName("processResources",ProcessResources::class) {
    val map = mapOf<String, String>(
        "modid" to rockbreaker.mod.id,
        "id" to rockbreaker.mod.id,
        "name" to rockbreaker.mod.displayName,
        "display_name" to rockbreaker.mod.displayName,
        "version" to rockbreaker.mod.version,
        "description" to rockbreaker.mod.description,
        "authors" to rockbreaker.mod.authors,
        "github_url" to rockbreaker.mod.sourceUrl,
        "source_url" to rockbreaker.mod.sourceUrl,
        "license" to rockbreaker.mod.license,
        "website" to rockbreaker.mod.generalWebsite,
        "issue_tracker" to rockbreaker.mod.issueTracker,
        "icon" to rockbreaker.mod.icon,
        "fabric_common_entry" to rockbreaker.modFabric.commonEntry,
        "fabric_client_entry" to rockbreaker.modFabric.clientEntry,
        "mc_min" to rockbreaker.env.mcVersion.min,
        "mc_max" to rockbreaker.env.mcVersion.max,
        "java_ver" to rockbreaker.env.javaVer.toString(),
        "loader_id" to rockbreaker.env.loader,
        "forgelike_loader_ver" to rockbreaker.dynamics.forgelikeLoaderVer(),
        "forgelike_api_ver" to rockbreaker.dynamics.forgelikeAPIVer(),
        "mixin_field" to rockbreaker.dynamics.mixinField(),
        "dependencies_field" to rockbreaker.dynamics.dependenciesField()
    )
    map.forEach { (key, value) ->
        inputs.property(key, value)
    }
    rockbreaker.dynamics.excludes().forEach { file ->
        exclude(file)
    }
    filesMatching("pack.mcmeta") { expand(map) }
    filesMatching("fabric.mod.json") { expand(map) }
    filesMatching("META-INF/mods.toml") { expand(map) }
    filesMatching("META-INF/neoforge.mods.toml") { expand(map) }
    rockbreaker.modMixins.getMixins(rockbreaker.env.type).forEach { str ->
        filesMatching(str) { expand(map) }
    }
}

java {
    withSourcesJar()
    val java =
        when (rockbreaker.env.javaVer) {
            8 -> JavaVersion.VERSION_1_8
            16 -> JavaVersion.VERSION_16
            17 -> JavaVersion.VERSION_17
            21 -> JavaVersion.VERSION_21
            else -> throw Error("Unknown java version used")
        }
    targetCompatibility = java
    sourceCompatibility = java
}

//TODO: Enable auto-publishing.
/*publishMods {
    file = tasks.remapJar.get().archiveFile
    additionalFiles.from(tasks.remapSourcesJar.get().archiveFile)
    displayName = "${mod.displayName} ${mod.version} for ${rockbreaker.env.mcVersion.min}"
    version = mod.version
    changelog = rootProject.file("CHANGELOG.md").readText()
    type = STABLE
    modLoaders.add(rockbreaker.env.loader)

    dryRun = modPublish.dryRunMode

    modrinth {
        projectId = modPublish.modrinthProjectToken
        // Get one here: https://modrinth.com/settings/pats, enable read, write, and create Versions ONLY!
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.addAll(modPublish.mcTargets)
        apis.forEach{ src ->
            if(src.enabled) src.versionRange.ifPresent{ ver ->
                if(src.type.isOptional()){
                    src.modInfo.rinthSlug?.let {
                        optional {
                            slug = it
                            version = ver.min

                        }
                    }
                }
                else{
                    src.modInfo.rinthSlug?.let {
                        requires {
                            slug = it
                            version = ver.min
                        }
                    }
                }
            }
        }
    }

    curseforge {
        projectId = modPublish.curseforgeProjectToken
        // Get one here: https://legacy.curseforge.com/account/api-tokens
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.addAll(modPublish.mcTargets)
        apis.forEach{ src ->
            if(src.enabled) src.versionRange.ifPresent{ ver ->
                if(src.type.isOptional()){
                    src.modInfo.curseSlug?.let {
                        optional {
                            slug = it
                            version = ver.min

                        }
                    }
                }
                else{
                    src.modInfo.curseSlug?.let {
                        requires {
                            slug = it
                            version = ver.min
                        }
                    }
                }
            }
        }
    }
}
// TODO Disable if not uploading to a maven
publishing {
    repositories {
        // TODO this is an example of how I recommend you do this.
        if(modPublish.mavenURL.isPresent) {
            maven {
                url = uri(modPublish.mavenURL.get())
                credentials {
                    username = System.getenv("MVN_NAME")
                    password = System.getenv("MVN_KEY")
                }
            }
        }
    }
    publications {
        create<MavenPublication>("mavenJava"){
            groupId = project.group.toString()
            artifactId = rockbreaker.env.archivesBaseName
            version = project.version.toString()
            from(components["java"])
        }
    }
}*/